grammar statemachine.dsl.MachineDsl with org.eclipse.xtext.common.Terminals

import "statemachine.model"
import "http://www.eclipse.org/emf/2002/Ecore"

Machine: 'machine' name=ID '{' states+=State* '}';

State: initial?='initial'? 'state' name=ID '{' handlers+=EventHandler* '}';

enum NormalEventHandlerKind returns EventHandlerKind: NORMAL='event';
enum EntryEventHandlerKind returns EventHandlerKind: ENTRY='entry';
enum ExitEventHandlerKind returns EventHandlerKind: EXIT='exit';

EventHandler: 
	kind=(NormalEventHandlerKind|EntryEventHandlerKind|ExitEventHandlerKind) 
	name=ID? '{' statements+=Statement* '}'
;

Statement: PrintStatement | JumpStatement;

PrintStatement: 'print' text=STRING ';';

JumpStatement: 'jump' target=[State] ';';
